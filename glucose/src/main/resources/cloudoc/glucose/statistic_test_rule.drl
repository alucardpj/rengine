package cloudoc.glucose;

//fbg count
rule "fbg count test"
    ruleflow-group "statistictest"
    salience 10

	when
	    $s : Statistictest()
		Number( $count : intValue )
		    from accumulate( Record( acktype in (0, 2, 4) ), count() )
	then
        $s.setFbgCount( $count );
end

//fbg normal count
rule "fbg normal count test"
    ruleflow-group "statistictest"

    when
        $s : Statistictest( )
		Number( $count : intValue )
		    from accumulate( Record( acktype in (0, 2, 4), level == 2 ), count() )
    then
        $s.setFbgNormalCount( $count );
end

//2 hpg count
rule "2 hpg count test"
    ruleflow-group "statistictest"

	when
	    $s : Statistictest()
		Number( $count : intValue )
		    from accumulate( Record( acktype in (1, 3, 5) ), count() )
	then
        $s.setTwoHpgCount( $count );
end

//2 hpg normal count
rule "2 hpg normal count test"
    ruleflow-group "statistictest"

    when
        $s : Statistictest()
		Number( $count : intValue )
		    from accumulate( Record( acktype in (1, 3, 5), level == 2 ), count() )
    then
        $s.setTwoHpgNormalCount( $count );
end

//hypoglycemia count
rule "hypoglycemia count test"
    ruleflow-group "statistictest"
    
    when
        $s : Statistictest()
        Number( $count : intValue )
            from accumulate( Record( value <= 3.9 ), count() )
    then
        $s.setHypoglycemiaCount( $count );
end

//hyperglycemia count
rule "hyperglycemia count test"
    ruleflow-group "statistictest"
    
    when
        $s : Statistictest()
        Number( $count : intValue )
            from accumulate( Record( value > 16.7 ), count() )
    then
        $s.setHyperglycemiaCount( $count );
end

//average FBG
rule "average FBG test"
    ruleflow-group "statistictest"
    salience 10
    
    when
        $s : Statistictest()
        accumulate( Record( acktype in (0, 2, 4), $value : value ),
                    $avg : average( $value ) )
    then
        $s.setAverageFBG($avg.doubleValue());
end

//average 2 Hpg
rule "average 2 Hpg test"
    ruleflow-group "statistictest"
    salience 10
    
    when
        $s : Statistictest( )
        accumulate( Record( acktype in (1, 3, 5), $value : value ),
                    $avg : average( $value ) )
    then
        $s.setAverageTwoHpg($avg.doubleValue());
end

//fbg variance
rule "fbg variance test"
    ruleflow-group "statistictest"
    
    when
        $s : Statistictest()
        Number( $var : doubleValue )
            from accumulate( Record( acktype in (0, 2, 4), $value : value ),
                    init( double variance = 0.0; ),
                    action( variance +=  ($value - $s.getAverageFBG())*($value - $s.getAverageFBG()); ),
                    reverse( variance -= ($value - $s.getAverageFBG())*($value - $s.getAverageFBG()); ),
                    result( new Double( variance / $s.getFbgCount() ) ) )
    then
        $s.setFbgVariance( $var );
        $s.setFbgStandardDeviation( Math.sqrt($var) );
end

//2 hpg variance
rule "2 hpg variance test"
    ruleflow-group "statistictest"
    
    when
        $s : Statistictest()
        Number( $var : doubleValue )
            from accumulate( Record( acktype in (1, 3, 5), $value : value ),
                    init( double variance = 0.0; ),
                    action( variance +=  ($value - $s.getAverageTwoHpg())*($value - $s.getAverageTwoHpg()); ),
                    reverse( variance -= ($value - $s.getAverageTwoHpg())*($value - $s.getAverageTwoHpg()); ),
                    result( new Double( variance / $s.getTwoHpgCount() ) ) )
    then
        $s.setTwoHpgVariance( $var );
        $s.setTwoHpgStandardDeviation( Math.sqrt($var) );
end